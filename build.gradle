plugins {
    id "net.minecraftforge.gradle.forge" version "6f53277"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "net.kyori.blossom" version "1.3.0"
    id "java"
}

version = mod_version
group = "com.example"
archivesBaseName = mod_name

blossom {
    String className = "src/main/java/com/example/template/ForgeTemplate.java"
    replaceToken("@VER@", project.version, className)
    replaceToken("@NAME@", mod_name, className)
    replaceToken("@ID@", mod_id, className)
}

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false

    clientRunArgs += '--tweakClass gg.essential.loader.stage0.EssentialSetupTweaker'
    clientJvmArgs += '-Dfml.coreMods.load=cc.woverflow.wcore.tweaker.WCoreTweaker'
}

configurations {
    // Creates an extra configuration that implements `implementation` to be used later as the configuration that shades libraries
    include
    implementation.extendsFrom(include)
}

repositories {
    maven { url 'https://repo.woverflow.cc/' }
}

dependencies {
    include ('gg.essential:loader-launchwrapper:1.1.3') {
        transitive = false
    }
    compileOnly 'gg.essential:essential-1.8.9-forge:1852'
    include ('cc.woverflow:w-core-tweaker:1.0.2') {
        transitive = false
    }
    compileOnly 'cc.woverflow:w-core:1.1.3'
}

/**
 * This task simply replaces the `${version}` and `${mcversion}` properties in the mcmod.info with the data from Gradle
 */
processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", version
    inputs.property "mcversion", minecraft.version
    inputs.property "name", mod_name
    inputs.property "id", mod_id

    filesMatching("mcmod.info") {
        expand(
                "id": mod_id,
                "name": mod_name,
                "version": version,
                "mcversion": minecraft.version
        )
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

sourceSets {
    main {
        output.resourcesDir = java.outputDir
    }
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

reobf {
    shadowJar {}
}

jar.dependsOn shadowJar
jar {
    manifest.attributes(
            'ModSide': 'CLIENT',
            'ForceLoadAsMod': true,
            'TweakClass': 'gg.essential.loader.stage0.EssentialSetupTweaker',
            "FMLCorePlugin": "cc.woverflow.wcore.tweaker.WCoreTweaker",
            "TweakOrder": "0",
            "FMLCorePluginContainsFMLMod": true
    )
    enabled = false
}